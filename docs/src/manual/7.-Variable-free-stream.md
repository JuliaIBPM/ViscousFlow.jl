```@meta
EditURL = "../../../test/literate/7.-Variable-free-stream.jl"
```

# Variable free stream
In this notebook we will simulate the flow with a time-varying free stream past a
stationary body. To demonstrate this, we will solve for oscillatory flow past a
rectangular object, in which the $x$ component of the free stream is

$$U_\infty(t) = A \sin(\Omega t)$$

````@example 7.-Variable-free-stream
using ViscousFlow
````

````@example 7.-Variable-free-stream
using Plots
using Statistics
````

### Problem specification
We will set the Reynolds number to 200

````@example 7.-Variable-free-stream
my_params = Dict()
my_params["Re"] = 200
````

In order to set a time-varying free stream, we have to define a function
that provides the instantaneous free stream components and pass that
function into the system definition. In this function, we will
use the `OscillatoryDOF` function (available via the [RigidBodyTools.jl](https://github.com/JuliaIBPM/RigidBodyTools.jl) package)
to create the modulated free stream. To demonstrate its possibilities,
we will pass in the parameters for the sinusoid via the `my_params` dictionary.
The "freestream average" specifies a mean free stream, if desired.

````@example 7.-Variable-free-stream
my_params["freestream average"] = 0.0
my_params["freestream frequency"]  = 2.0
my_params["freestream amplitude"] = 0.5
my_params["freestream phase"] = π/2
````

Now we define the function. We can call it anything we want,
but it has to have the argument signature as shown. The
`Sinusoid` function is used, with the shift operator `>>`
to apply any phase lag.

````@example 7.-Variable-free-stream
function my_freestream(t,phys_params)
    U = phys_params["freestream average"]
    Ω = phys_params["freestream frequency"]
    Ax = phys_params["freestream amplitude"]
    ϕx = phys_params["freestream phase"]
    kin = OscillatoryDOF(Ax,Ω,ϕx,U)

    Vinf_angle = get(phys_params,"freestream angle",0.0)
    Vinf_amp = dof_velocity(kin(t))

    Uinf = Vinf_amp*cos(Vinf_angle)
    Vinf = Vinf_amp*sin(Vinf_angle)
    return Uinf, Vinf
end
````

The freestream function is passed in via the "freestream" key in the
parameters Dict.

````@example 7.-Variable-free-stream
my_params["freestream"] = my_freestream
````

Now let us carry on with the other usual steps:

### Discretize

````@example 7.-Variable-free-stream
xlim = (-2.0,2.0)
ylim = (-1.5,1.5)
my_params["grid Re"] = 4.0
g = setup_grid(xlim,ylim,my_params)
````

### Set up bodies
Here, we will set up an ellipse in the center of the domain

````@example 7.-Variable-free-stream
Δs = surface_point_spacing(g,my_params)
body = Ellipse(0.25,0.5,Δs)

joint = Joint(RigidTransform([0.0,0.0],0.0))
m = RigidBodyMotion(joint,body)
x = init_motion_state(body,m)
update_body!(body,x,m)
````

````@example 7.-Variable-free-stream
plot(body,xlim=xlim,ylim=ylim)
````

### Construct the system structure
This step is like the previous notebooks:

````@example 7.-Variable-free-stream
sys = viscousflow_system(g,body,phys_params=my_params,motions=m);
nothing #hide
````

### Initialize
Now, we initialize with zero vorticity

````@example 7.-Variable-free-stream
u0 = init_sol(sys)
````

and create the integrator

````@example 7.-Variable-free-stream
tspan = (0.0,10.0)
integrator = init(u0,tspan,sys)
````

### Solve
Now we are ready to solve the problem. Let's advance the solution to $t = 2.5$.

````@example 7.-Variable-free-stream
@time step!(integrator,2.5)
````

### Examine
Let's look at the flow field at the end of this interval

````@example 7.-Variable-free-stream
sol = integrator.sol
plt = plot(layout = (2,2), size = (800, 600), legend=:false)
tsnap = 1.0:0.5:2.5
for (i, t) in enumerate(tsnap)
    plot!(plt[i],vorticity(sol,sys,t),sys,layers=false,title="t = $(round(t,digits=2))",clim=(-10,10),levels=range(-10,10,length=30),color = :RdBu)
    plot!(plt[i],surfaces(sol,sys,t))
end
plt
````

#### Compute the force history
Just as we did for the stationary body in a constant free stream

````@example 7.-Variable-free-stream
mom, fx, fy = force(sol,sys,1);
nothing #hide
````

Plot them

````@example 7.-Variable-free-stream
plot(
plot(sol.t,2*fx,xlim=(0,Inf),ylim=(-6,6),xlabel="Convective time",ylabel="\$C_D\$",legend=:false),
plot(sol.t,2*fy,xlim=(0,Inf),ylim=(-6,6),xlabel="Convective time",ylabel="\$C_L\$",legend=:false),
    size=(800,350)
)
````

The mean drag and lift coefficients are

````@example 7.-Variable-free-stream
meanCD = mean(2*fx[3:end])
````

````@example 7.-Variable-free-stream
meanCL = mean(2*fy[3:end])
````

---

*This page was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*

